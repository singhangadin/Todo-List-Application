plugins {
    id 'kotlin-kapt'
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.todo_list_application"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "ui_type"
    productFlavors {
        xmlUI {
            dimension "ui_type"
            applicationIdSuffix ".xml"
            versionNameSuffix "-xml"

            buildFeatures {
                compose false
                dataBinding true
            }
        }
        composeUI {
            dimension "ui_type"
            applicationIdSuffix ".compose"
            versionNameSuffix "-compose"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.0"
        kotlinCompilerVersion = "1.7.10"
    }
    buildFeatures {
        compose true
        dataBinding true
    }
    buildToolsVersion '33.0.0'
}

dependencies {
    def nav_version = "2.5.1"
    def room_version = "2.4.3"
    def lifecycle_version = "2.5.1"

    // Local Modules
    implementation project(path: ':data')
    implementation project(path: ':domain')
    implementation project(path: ':common')

    xmlUIImplementation project(path: ':ui-xml')
    composeUIImplementation project(path: ':ui-compose')
    implementation 'androidx.activity:activity-compose:1.5.1'

    // Core
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation "androidx.fragment:fragment-ktx:1.5.2"
    implementation 'com.google.android.material:material:1.6.1'

    // Network
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.43.2'
    kapt 'com.google.dagger:hilt-compiler:2.43.2'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Room
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycle_version}"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}